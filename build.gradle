plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.1'
}

group = 'com.hyperfactions'
version = '0.2.0'

// Shared version property avoids accessing project at execution time
def buildVersion = objects.property(String).convention(version)

// Override version for dev builds
gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(':buildDev')) {
        project.version = '0.0.0'
        buildVersion.set('0.0.0')
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Hytale Server API (provided at runtime)
    compileOnly files(rootProject.hytaleServerJar)

    // HyperPerms soft dependency (for compile-time reference only)
    // Actual integration uses reflection to avoid hard dependency
    compileOnly files('../HyperPerms/build/libs/HyperPerms-2.4.5.jar')

    // JSON handling
    implementation 'com.google.code.gson:gson:2.11.0'

    // Null safety annotations
    compileOnly 'org.jetbrains:annotations:24.1.0'
    testCompileOnly 'org.jetbrains:annotations:24.1.0'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// Generate BuildInfo.java with project version
def generatedSrcDir = layout.buildDirectory.dir("generated/sources/buildinfo/java")
sourceSets.main.java.srcDir(generatedSrcDir)

tasks.register('generateBuildInfo') {
    def outputDir = generatedSrcDir
    def ver = buildVersion
    inputs.property('version', ver)
    outputs.dir(outputDir)
    doLast {
        def v = ver.get()
        def dir = outputDir.get().asFile.toPath().resolve("com/hyperfactions").toFile()
        dir.mkdirs()
        new File(dir, "BuildInfo.java").text = """\
package com.hyperfactions;

/**
 * Auto-generated build information.
 */
public final class BuildInfo {
    public static final String VERSION = "${v}";
    public static final String JAVA_VERSION = "${System.getProperty('java.version')}";
    public static final long BUILD_TIMESTAMP = ${System.currentTimeMillis()}L;
    private BuildInfo() {}
}
"""
    }
}

// Expand version placeholder in manifest.json
processResources {
    def ver = buildVersion
    inputs.property('version', ver)
    filesMatching('manifest.json') {
        expand(version: ver.get())
    }
}

shadowJar {
    archiveClassifier.set('')

    // Relocate dependencies to avoid conflicts
    relocate 'com.google.gson', 'com.hyperfactions.lib.gson'

    // Don't minimize - it removes Gson's inner classes needed at runtime
}

test {
    useJUnitPlatform()
}

build {
    dependsOn shadowJar
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.addAll(['-Xlint:all', '-Xlint:-processing'])
}

// Ensure HyperPerms is built before compiling HyperFactions
tasks.named('compileJava') {
    dependsOn ':HyperPerms:shadowJar'
    dependsOn 'generateBuildInfo'
}

// Dev build task - clean build with version set to 'dev'
tasks.register('buildDev') {
    group = 'build'
    description = 'Clean build with dev version identifier'
    dependsOn 'clean', 'build'
}

tasks.named('build') {
    mustRunAfter 'clean'
}
